apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = project.property('VERSION_NAME') as String
group = 'com.staticbloc.media'

android {
  compileSdkVersion project.property("COMPILE_SDK") as int
  buildToolsVersion project.property("BUILD_TOOLS_VERSION") as String

  defaultConfig {
    minSdkVersion project.property("MIN_SDK") as int
    targetSdkVersion project.property("TARGET_SDK") as int
    versionCode project.property("VERSION_CODE") as int
    versionName project.property("VERSION_NAME") as String
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.drewnoakes:metadata-extractor:2.8.1'
  compile('com.staticbloc.media:ui:0.0.1@aar') {
    transitive = true
  }
  testCompile 'junit:junit:4.12'
}

bintray {
  user = project.property('BINTRAY_USER')
  key = project.property('BINTRAY_KEY')
  configurations = ['archives']
  publish = true
  pkg {
    repo = 'media'
    name = 'camera'
    userOrg = 'staticbloc'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/staticbloc/media.git'
    publicDownloadNumbers = true
    version {
      name = project.property('VERSION_NAME')
      released  = new Date()
      vcsTag = project.property('VERSION_NAME')
    }
  }
}

install {
  repositories.mavenInstaller {
    pom {
      project {
        packaging 'aar'

        name 'Simple Camera'
        description 'A facade for making it easy to use the camera in android'
        url 'https://github.com/staticbloc/media/camera'
        inceptionYear '2016'

        licenses {
          license {
            name 'The MIT License (MIT)'
          }
        }
        developers {
          developer {
            id 'eygraber'
            name 'Eliezer Graber'
          }
        }
        scm {
          connection 'https://github.com/staticbloc/media.git'
          url 'https://github.com/staticbloc/media/camera'
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
artifacts {
  archives javadocJar
  archives sourcesJar
}

apply from: "${rootDir.getAbsolutePath()}/gradle/scripts/allscripts.gradle"
